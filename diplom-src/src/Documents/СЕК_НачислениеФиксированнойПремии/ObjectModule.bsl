
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда  

#Область ОбработчикиСобытийМодуляОбъекта

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр СЕК_ДополнительныеНачисления
	Движения.СЕК_ДополнительныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаСотрудники Из Сотрудники Цикл
		Движение = Движения.СЕК_ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = ТекСтрокаСотрудники.Сотрудник;
		Движение.Подразделение = Подразделение;
		Движение.ВидРасчета = ПланыВидовРасчета.СЕК_ДополнительныеНачисления.Премия; 
		Движение.Сумма = ТекСтрокаСотрудники.СуммаПремии;
	КонецЦикла;
	
	Движения.СЕК_ДополнительныеНачисления.Записать();
	
	Движения.СЕК_Удержания.Записывать = Истина;
	
	РасчетУдержаний();
	
	СформироватьДвиженияВзаиморасчетыССотрудниками(); 

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура РасчетУдержаний()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СЕК_НачислениеФиксированнойПремииСотрудники.Сотрудник КАК Сотрудник
	|ИЗ
	|	Документ.СЕК_НачислениеФиксированнойПремии.Сотрудники КАК СЕК_НачислениеФиксированнойПремииСотрудники
	|ГДЕ
	|	СЕК_НачислениеФиксированнойПремииСотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.СЕК_Удержания.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ПланыВидовРасчета.СЕК_Удержания.НДФЛ;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Подразделение = Подразделение;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
	КонецЦикла;
	
	Движения.СЕК_Удержания.Записать();
	
	РасчетСуммыУдержаний();
	
КонецПроцедуры

Процедура РасчетСуммыУдержаний()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СЕК_Удержания.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(СЕК_УдержанияБазаСЕК_ДополнительныеНачисления.СуммаБаза, 0) КАК СуммаБаза
	|ИЗ
	|	РегистрРасчета.СЕК_Удержания КАК СЕК_Удержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.СЕК_Удержания.БазаСЕК_ДополнительныеНачисления(
	|				&Измерения,
	|				&Измерения,
	|				,
	|				Регистратор = &Ссылка
	|					И ВидРасчета = &ВидРасчета) КАК СЕК_УдержанияБазаСЕК_ДополнительныеНачисления
	|		ПО СЕК_Удержания.НомерСтроки = СЕК_УдержанияБазаСЕК_ДополнительныеНачисления.НомерСтроки
	|ГДЕ
	|	СЕК_Удержания.Регистратор = &Ссылка";
	
	Измерение = Новый Массив; 
	Измерение.Добавить("Сотрудник");
	Измерение.Добавить("Подразделение");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Измерения", Измерение);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.СЕК_Удержания.НДФЛ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.СЕК_Удержания[Выборка.НомерСтроки - 1];
		Движение.Сумма = Выборка.СуммаБаза * 13 / 100;
	КонецЦикла;
	
	Движения.СЕК_Удержания.Записать(, Истина);
	
КонецПроцедуры

Процедура СформироватьДвиженияВзаиморасчетыССотрудниками()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СЕК_Удержания.Сумма КАК НДФЛ,
		|	СЕК_ДополнительныеНачисления.Сотрудник КАК Сотрудник,
		|	СУММА(СЕК_ДополнительныеНачисления.Сумма) КАК Сумма
		|ИЗ
		|	РегистрРасчета.СЕК_ДополнительныеНачисления КАК СЕК_ДополнительныеНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.СЕК_Удержания КАК СЕК_Удержания
		|		ПО СЕК_ДополнительныеНачисления.Сотрудник = СЕК_Удержания.Сотрудник
		|ГДЕ
		|	СЕК_Удержания.Регистратор = &Ссылка
		|	И СЕК_ДополнительныеНачисления.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СЕК_ДополнительныеНачисления.Сотрудник,
		|	СЕК_Удержания.Сумма
		|ИТОГИ
		|	СУММА(НДФЛ),
		|	СУММА(Сумма)
		|ПО
		|	Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Сотрудник");
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Сумма <> 0 Тогда
		Движение = Движения.СЕК_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Подразделение = Подразделение;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.Сумма - Выборка.НДФЛ;
		КонецЕсли; 
	КонецЦикла;
	
	Движения.СЕК_ВзаиморасчетыССотрудниками.Записать();

КонецПроцедуры

#КонецОбласти

#КонецЕсли
